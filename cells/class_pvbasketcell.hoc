begintemplate pvbasketcell
public init, connect_sections, size_sections, append_sections
public mechinit, insert_mechs, set_biophys, get_root
public  pre_list, connect_pre, is_art, is_connected, gid, randi
public soma, dend
public all, basal_list, apical_list, soma_list, axon_list, dendrite_list
public x, y, z, position, myroot, Vrest
public NumSoma, NumApical, NumBasal, NumAxon

// strings
strdef myroot

// objects
objref syn, pre_list, templist, rootlist, this, rr, rv

// external variables
external numCellTypes, cellType

// create the sections[segments]
NumSoma=1
NumApical=2
NumBasal=2
NumDend = NumApical + NumBasal
NumAxon=0
create soma[NumSoma], dend[NumDend]


proc init() {
	gid = $1
	randi = $2
	
	// morphology
	connect_sections()	// local fcn: connect soma, dendrites, axon initial segment
	size_sections()		// local fcn: set the size dimensions of each section
	define_shape()		// builtin fcn: fill in 3d info for sections defined by only L and diam, translate 3d points for consistency with their connections 
  	append_sections()	// local fcn: append all sections to the section list
	set_nseg()			// local fcn: set the number of segments in each section
	get_root()			// local fcn: perform morphology checks

	// electrophysiology
	mechinit(gid)			// local fcn: set values for max conductances and reversal potentials of ion channels and other ephys parameters that are subject to fitting
	insert_mechs()		// local fcn: insert ion channels and actually set values determined in the mechinit fcn
	set_chanparams()	// local fcn: after all channels have been inserted, then their other parameters can be set	
	// synapses
	pre_list = new List() // define a list for the presynaptic connections
	define_synapses($3)	// local fcn: define all possible synaptic connections received by this cell
	//add_noise(gid)
}

proc connect_sections() { local i
 	connect dend[0](0), soma(1)
	connect dend[1](0), soma(1)
	connect dend[2](0), soma(0)
	connect dend[3](0), soma(0)
}

proc size_sections() {
	soma[0] {pt3dclear()
		pt3dadd(0, 0, 0, 10) // distance from (0,0,0) = 0
		pt3dadd(0, 10, 0, 10) // distance from (0,0,0) = 10
		pt3dadd(0, 20, 0, 10) // distance from (0,0,0) = 20
	}
	dend[0] {pt3dclear()
		pt3dadd(0, 20, 0, 3) 
		pt3dadd(10, 69, 0, 3) 
		pt3dadd(20, 118, 0, 3)
	}
	dend[1] {pt3dclear()
		pt3dadd(0, 20, 0, 3) 
		pt3dadd(-10, 69, 0, 3) 
		pt3dadd(-20, 118, 0, 3)
	}
	dend[2] {pt3dclear()
		pt3dadd(0, 0, 0, 3) 
		pt3dadd(10, -49, 0, 3) 
		pt3dadd(20, -98, 0, 3)
	}
	dend[3] {pt3dclear()
		pt3dadd(0, 0, 0, 3) 
		pt3dadd(-10, -49, 0, 3) 
		pt3dadd(-20, -98, 0, 3)
	}
}

objref all, basal_list, apical_list, dendrite_list, soma_list, axon_list
proc append_sections() { local i
	objref all, basal_list, apical_list, dendrite_list, soma_list, axon_list

	all = new SectionList()
	basal_list = new SectionList()
	apical_list = new SectionList()
	soma_list = new SectionList()
	axon_list = new SectionList()
	dendrite_list = new SectionList()

	soma all.append()
	soma soma_list.append()
	for i=0,3 {
		dend[i] all.append()
		dend[i] dendrite_list.append()
	}

	for i=0,1 {
		dend[i] apical_list.append()
	}

	for i=2,3 {
		dend[i] basal_list.append()
	}
}


external lambda_f
proc set_nseg() {
  	forsec all { nseg = int((L/(0.1*lambda_f(100))+.9)/2)*2 + 1  }
}



proc mechinit() {
	objref rv, rr
	rr = new Random($1+12)
	rv = new Random($1+199)

	// resting membrane potential. Must lie between Na+ and K+ reversal potentials
	Vrest = rv.normal(-65,5) //mean, variance
	
	// Temperature of simulation
	celsius = 34.0  

	// Membrane resistance in ohm*cm2
	randrm = rr.normal(6000,5760000) //std of 2400
	if(randrm<2000){randrm = 2000} // minimum rm is 2000
	
	RmDend = 20000 //1/0.00018 
	RmSoma = 20000 //1/0.00018 

	// Membrane capacitance in uF/cm2
	CmSoma= 1
	CmDend = 1

	// Axial resistance in ohm*cm
	RaDend= 150
	RaSoma= 150*22.5	
	
	// Calcium concentrations in mM
	ca_outside = 2
	ca_inside = 5.e-6 // 50.e-6
	catau = 10

	// reversal potentials in mV
	ekval = -80	
	enaval = 40
	eHCNval = -30
	ecaval = 8.314*(273.15+celsius)/(2*9.649e4)*log(ca_outside/ca_inside)*1000 // about 170, otherwise set to 130
	
	if (Vrest<ekval) Vrest=ekval // Cell cannot rest lower than K+ reversal potential
	if (Vrest>enaval) Vrest=enaval // Cell cannot rest higher than Na+ reversal potential
	eleakval = Vrest

	// max ion channel conductances in mho/cm2
	dropout = rr.uniform(0,1)
	if(dropout<0){
		print "dropping out cell ", $1
		gNav_soma = 0
		gNav_dend = 0
	}else{
		gNav_soma   =   0.035//0.15 // soma: // 0.12 //original 0.030 to .055 ; lm: //0.5  	//original 0.015
		gNav_dend	=   0.01 //0.102
	}

	gNap_soma	= 0
	gNap_dend	= 0
	
	gKdr_soma   = 0.035 // Delayed rectifier potassium
	gKdr_dend   = 0.003
	
	gKvA_soma 	= 0 //0.02 //0.00015 // Proximal A-type potassium
	gKvA_dend 	= 0
	
	gCavN_soma  = 0//6.73e-005//0.0008 //   T-type calcium
	gCavN_dend	=  0//4.85e-005//0.000283 //
	
	gCavL_soma  = 0//0.0021 //  L-type calcium
	gCavL_dend  =  0//0.000356 //0.000735 //  L-type calcium
}

proc insert_mechs() {

	forsec all {
		/*
		insert ch_KvA
		gmax_ch_KvA = gKvA		// A-type K+ conductance
		
		insert ch_CavN  			// N-type Ca2+ conductance
		gmax_ch_CavN = gCavN
		
		insert ch_CavL 
		gmax_ch_CavL = gCavL
		
		insert ch_KCaS
		gmax_ch_KCaS = gKCaS
		
		insert ch_KvCaB
		gmax_ch_KvCaB = gKvCaB
		*/
		Ra = RaSoma
	} 

	soma {
		
		insert nainter	
		gnabar_nainter = gNav_soma
		
		insert kdrinter
		gkdrbar_kdrinter = gKdr_soma
		
		//insert ch_Nav
		//gmax_ch_Nav = gNav_soma
		
		insert nap
		gbar_nap = gNap_soma
		
		//insert ch_Kdrfast
		//gmax_ch_Kdrfast = gKdr_soma
		
		insert ch_CavN  			// N-type Ca2+ conductance
		gmax_ch_CavN = gCavN_soma
		
		insert ch_CavL
		gmax_ch_CavL = gCavL_soma
		
		//insert natraub
		//gbar_natraub = 187.5
		
		//insert kdrtraub
		//gbar_kdrtraub = 125
		
		insert leakinter
		glbar_inter_leakinter = 1/RmSoma
		
		el_leakinter = Vrest
		
		cm=CmSoma
		ena = enaval
		ek = ekval
		
		eca = ecaval
		cao_iconc_Ca = ca_outside
	} 

	forsec dendrite_list {
		
		//insert ch_Nav
		//gmax_ch_Nav = gNav_dend
		
		insert nainter	
		gnabar_nainter = gNav_dend
		
		insert kdrinter
		gkdrbar_kdrinter = gKdr_dend
		
		insert nap
		gbar_nap = gNap_dend
		
		//insert ch_Kdrfast
		//gmax_ch_Kdrfast = gKdr_dend
		
		insert ch_CavN  			// N-type Ca2+ conductance
		gmax_ch_CavN = gCavN_dend
		
		insert ch_CavL
		gmax_ch_CavL = gCavL_dend
		
		
		insert leakinter
		glbar_inter_leakinter = 1/RmDend
		
		el_leakinter = Vrest
		cm=CmDend
		
		eca = ecaval
		cao_iconc_Ca = ca_outside
	}
}

proc set_chanparams() {
	forsec all {
		//ena = enaval
		//ek = ekval
		//eca = ecaval
		//e_ch_leak = eleakval
		//cao_iconc_Ca = ca_outside
	}
}


proc connect_pre() {  // $o1 target point process, $o2 returned NetCon
	soma $o2 = new NetCon (&v(1), $o1)
	$o2.threshold = -10

}

objref stim[100000],noise_syn[100000],noise_nc[100000],rnoise

proc add_noise(){
	rnoise = new Random($1+12)
        //Create NetStim
	g = $1
	for i=1,100{
		f = 5 + rnoise.uniform(0,1) // Hz
		stim[i] = new NetStim()
		stim[i].interval = 1000/f
		stim[i].number = 1e10
		stim[i].start = 10
		stim[i].noise = 1

        	//Create synapse
		dend[0](1/i) noise_syn[i] = new MyExp2Sid(0.5)
		noise_syn[i].tau1 = 0.132
		noise_syn[i].tau2 = 3.74
		noise_syn[i].e = 0

        	//Use NetCon to connect them
		noise_nc[i] = new NetCon(stim[i], noise_syn[i])
		noise_nc[i].weight = 0.002
	}
}



func is_art()  { return 0 }

proc position(){ local i
	forall {
		for i = 0, n3d()-1 {
			pt3dchange(i, $1-x+x3d(i), $2-y+y3d(i), $3-z+z3d(i), diam3d(i))
		}
	}
	x = $1  y = $2  z = $3	
}

proc get_root() {local i localobj sref
	rootlist = new SectionList()
	rootlist.allroots()
	i=0
	forsec all {
		sref = new SectionRef()
		if (sref.has_parent==0) {
			myroot = secname()
			i=i+1
		}
		for(x,0) {
			if (diam(x) <=0.01) print "WARNING: tiny diameter of ",  diam(x), " um at ", secname(), ", point ", x, "!"
			if (diam3d(x) <=0.01) print "WARNING: tiny 3d diameter of ", diam3d(x), " um at ", secname(), ", point ", x, "!"
		}
		if (L <=0.001) print "WARNING: tiny length of ", L, " um at ", secname(), "!"
	}
	if (i>1) {
		print "WARNING: cell ", gid, " has ", i, " root sections!"
	}
}

strdef myStr

objref newSecRef, syn
proc define_synapses() {
	ind = $1
	i = 0

	access soma[0]
	{distance()}

	for celltype = 0, numCellTypes-1 {
		templist = new List ()
		for r=0, cellType[ind].SynList[celltype].count()-1 {
			execute(cellType[ind].SynList[celltype].object(r).NewSynStr, this) // sets newSecRef
						
			forsec newSecRef {		
				for (x,0) {
					execute(cellType[ind].SynList[celltype].object(r).CondStr, this)
					 if (y==1) {
						execute(cellType[ind].SynList[celltype].object(r).SynStr, this)
						if (cellType[ind].SynList[celltype].object(r).GABAabFlag==0) {
							syn.tau1 = cellType[ind].SynList[celltype].object(r).tau1
							syn.tau2 = cellType[ind].SynList[celltype].object(r).tau2
							syn.e = cellType[ind].SynList[celltype].object(r).efirst
							if (strcmp(cellType[ind].SynList[celltype].object(r).SynType,"MyExp2Sidnw")==0) {
								execute(cellType[ind].SynList[celltype].object(r).Scaling, this)
							}
						} else {
							syn.tau1a = cellType[ind].SynList[celltype].object(r).tau1a
							syn.tau2a = cellType[ind].SynList[celltype].object(r).tau2a
							syn.ea = cellType[ind].SynList[celltype].object(r).ea
							syn.tau1b = cellType[ind].SynList[celltype].object(r).tau1b
							syn.tau2b = cellType[ind].SynList[celltype].object(r).tau2b
							syn.eb = cellType[ind].SynList[celltype].object(r).eb
						}
						syn.sid = i
						templist.append(syn)
						i = i + 1
					}
				}
			}
		}
		pre_list.append(templist)
		findme = 1
	}
}

endtemplate pvbasketcell
